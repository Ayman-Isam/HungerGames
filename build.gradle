plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'io.papermc.paperweight.userdev' version '1.7.1'
}

group = 'me.aymanisam'
version = '1.5.0-1.20'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        url = "https://jitpack.io"
    }
    maven {
        url 'https://repo.codemc.io/repository/maven-releases/'
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.20.4-R0.1-SNAPSHOT"
    implementation 'commons-io:commons-io:2.11.0'
    implementation("org.bstats:bstats-bukkit:2.2.1")
    paperweight.paperDevBundle("1.20.4-R0.1-SNAPSHOT")
    compileOnly 'com.github.NEZNAMY:TAB-API:4.1.6'
    compileOnly 'com.github.retrooper:packetevents-spigot:2.4.0'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks.assemble {
    dependsOn(tasks.reobfJar)
}

shadowJar {
    archiveFileName = "Hungergames-1.5.0-1.20.jar"
    relocate 'org.bstats', "${group}.bstats"
    archiveClassifier.set('')
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
