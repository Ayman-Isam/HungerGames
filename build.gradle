plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.5'
}

group = 'me.aymanisam'
version = '1.7.4'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = "https://jitpack.io"
    }
    maven {
        url 'https://repo.codemc.io/repository/maven-releases/'
    }
    maven {
        name = "codemc-snapshot"
        url = "https://repo.codemc.io/repository/maven-snapshots/"
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.20-R0.1-SNAPSHOT'
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.5.0'
    implementation 'commons-io:commons-io:2.14.0'
    implementation 'org.bstats:bstats-bukkit:2.2.1'
    implementation 'com.github.retrooper:packetevents-spigot:2.6.0'
    implementation 'net.kyori:adventure-platform-bukkit:4.3.4'
    implementation 'com.googlecode.json-simple:json-simple:1.1.1'
    implementation "net.wesjd:anvilgui:1.10.3-SNAPSHOT"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

shadowJar {
    archiveFileName = "Hungergames-${version}.jar"
    relocate 'org.bstats', "${group}.bstats"
    relocate 'com.github.retrooper.packetevents', "${group}.packetevents.api"
    relocate 'io.github.retrooper.packetevents', "${group}.packetevents.impl"
    relocate 'net.wesjd.anvilgui', "${group}.anvilgui"
    archiveClassifier.set('')
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
